; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; IfDoubleClick v2.0
; Written by Alex Peters, 10/9/2003
; ______________________________________________________________________

; Unless a time is passed with the script call, we will assume that a
; double click would take place in this many milliseconds — if we wait
; this long and a second click isn't detected, we'll assume a single
; click.

    Local DblClickTimeout = 500

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; No user-serviceable parts follow! :-)
; ______________________________________________________________________

    Static Event
    Static OnSingleClick = ""

    If(Arg(3))
        DblClickTimeout = Arg(3)

    If(Arg(1)) Do
        If(Event) Do
            Event.Destroy(Event)
            Event = 0
            Do(Arg(1), "")
        Else
            OnSingleClick = Arg(2)
            Event = Event.CreateMS(DblClickTimeout, 1, '+
                ".IfDoubleClick@Event")
        EndIf
    Else
        If(Arg(2)) Do
            If(Event) Do
                Event.Destroy(Event)
                Event = 0
                Quit
            EndIf
            Event = Event.CreateMS(DblClickTimeout, 1, '+
                ".IfDoubleClick@Event")
            Do(Arg(2), "")
        Else
            Local Message = "IfDoubleClick v2.0'r'nWritten by Alex P" '+
                ++ "eters, 10/9/2003'r'n'r'nUsage:'r'n.IfDoubleClick" '+
                ++ "(OnDoubleClick, OnSingleClick[, DblClickTimeout]" '+
                ++ ")'r'n'r'nSpecify both a double- and single-click" '+
                ++ " command to give your bar button, tray icon, hot" '+
                ++ "key etc. different functionality depending on wh" '+
                ++ "ether it is executed once or twice in a given ti" '+
                ++ "meframe (as specifi"
            Message = Message ++ "ed by DblClickTimeout, or " ++ '+
                DblClickTimeout ++ " milliseconds by default).'r'n'r" '+
                ++ "'nSpecify only a double- and no single-click com" '+
                ++ "mand for your bar button etc. to only respond to" '+
                ++ " double clicks.'r'n'r'nSpecify only a single- an" '+
                ++ "d no double-click command for your bar button et" '+
                ++ "c. to perform that command, and then ignore any " '+
                ++ "accidental double-c"
            Message = Message ++ "lick that follows (technically, an" '+
                ++ "y second click within DblClickTimeout of the fir" '+
                ++ "st click, or " ++ DblClickTimeout ++ " milliseco" '+
                ++ "nds by default)."
            MessageBox("Information", Message, "IfDoubleClick v2.0")
        EndIf
    EndIf

    Quit

@Event

    Event = 0
    Do(OnSingleClick, "")
; ______________________________________________________________________