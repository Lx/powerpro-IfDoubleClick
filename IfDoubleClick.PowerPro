; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; IfDoubleClick v2.2
; Written by Alex Peters, 5/10/2004
; ______________________________________________________________________

; Unless a time is passed with the script call, we will assume that a
; double click would take place in this many milliseconds — if we wait
; this long and a second click isn't detected, we'll assume a single
; click.

    Local DblClickTimeout = 500
; ______________________________________________________________________

    Static Event, OnSingleClick = ""

    If(Arg(3))
        DblClickTimeout = Arg(3)

    If(Arg(1)) Do
        If(Event) Do
            Event = Event.Destroy(Event)
            Do(Arg(1))
        Else
            OnSingleClick = Arg(2)
            Event = Event.CreateMS(DblClickTimeout, 1, ?"." ++ ScriptName ++ ?"@Event")
        EndIf
    Else
        If(Arg(2)) Do
            If(Event) Do
                Event = Event.Destroy(Event)
                Quit
            EndIf
            Event = Event.CreateMS(DblClickTimeout, 1, ?"." ++ ScriptName ++ ?"@Event")
            Do(Arg(2))
        Else
            Local LF = Esc(?"\n", ?"\")
			Local Message = ?"IfDoubleClick v2.2\nWritten by Alex Peters, 5/10/2004" ++ LF ++ LF ++ ?"Usage:" ++ LF ++ ?"." ++ ScriptName ++ ?"(OnDoubleClick, OnSingleClick[, DblClickTimeout])" ++ LF ++ LF ++ ?"Specify both a double- and single-click command to give your bar button, tray icon, hotkey etc. different functionality depending on whether it is executed once or twice in a given timeframe (as specified by DblClickTimeout, or " ++ DblClickTimeout ++ ?" milliseconds by default)."
            Message = Message ++ LF ++ LF ++ ?"Specify only a double- and no single-click command for your bar button etc. to only respond to double clicks." ++ LF ++ LF ++ ?"Specify only a single- and no double-click command for your bar button etc. to perform that command, and then ignore any accidental double-click that follows (technically, any second click within DblClickTimeout of the first click, or " ++ DblClickTimeout ++ ?" milliseconds by default)."
            MessageBox(?"Information", Message, ?"IfDoubleClick v2.2")
        EndIf
    EndIf

    Quit

@Event

    Event = 0
    Do(OnSingleClick)
; ______________________________________________________________________
